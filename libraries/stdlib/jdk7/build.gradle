import java.nio.file.Files

description = 'Kotlin Standard Library JDK 7 extension'

apply plugin: 'kotlin'

JvmToolchain.configureJvmToolchain(project, JdkMajorVersion.JDK_1_8)

RepoArtifacts.publish(project)
RepoArtifacts.sourcesJar(project)
RepoArtifacts.javadocJar(project)

sourceSets {
    main {
        kotlin {
        }
    }
    test {
        kotlin {
        }
    }
    recursiveDeletionTest {
        kotlin {
            srcDir 'recursiveDeletionTest'
        }
    }
    java9 {
        java {
            srcDir 'java9'
        }
    }
}

dependencies {
    api project(':kotlin-stdlib')
    recursiveDeletionTestApi project(":kotlin-stdlib")
    recursiveDeletionTestApi project
    recursiveDeletionTestApi RepoDependencies.kotlinTest(project, "junit")
}

jar {
    LibrariesCommon.manifestAttributes(project, manifest, 'Main', true)
    from sourceSets.java9.output
}

sourcesJar {
    from sourceSets.java9.allSource
}

artifacts {
    archives sourcesJar
    sources sourcesJar
}

compileKotlin {
    kotlinJavaToolchain.toolchain.use(JvmToolchain.getToolchainLauncherFor(project, JdkMajorVersion.JDK_11_0))
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.moduleName = project.name
    kotlinOptions.freeCompilerArgs = [ "-Xjdk-release=7" ]
}

compileTestKotlin {
}

LibrariesCommon.configureJava9Compilation(project, 'kotlin.stdlib.jdk7')

// The test removes a directory inside a current working directory.
// This task isolates it from all other tests and provides it with a dedicated working directory.
check.dependsOn(tasks.register("recursiveDeletionTest", Test) {test ->
    test.dependsOn(recursiveDeletionTestClasses)
    test.group = "verification"
    test.testClassesDirs = sourceSets.recursiveDeletionTest.output.classesDirs
    test.classpath = files(sourceSets.recursiveDeletionTest.runtimeClasspath, tasks.jar)

    var buildDir = layout.buildDirectory.get().asFile.toPath()

    doFirst {
        test.workingDir(Files.createTempDirectory(buildDir, "rec-deletion-tests-"))
    }

    doLast {
        workingDir.deleteDir()
    }
})
